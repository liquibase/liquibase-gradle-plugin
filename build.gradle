#!groovy
// The above triggers groovy syntax highlighting in vim

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:${gradlePublishPluginVersion}"
    }
}
// Apply the Plugin Publish plugin to make plugin publication possible.  The Plugin Publish plugin
// will in turn auto-apply the Gradle Plugin Development Plugin (java-gradle-plugin) and the Maven
// Publish plugin (maven-publish).
plugins {
    id "com.vanniktech.maven.publish" version "0.32.0"
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: "groovy"
apply plugin: "signing"
apply plugin: "idea"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "org.liquibase"
archivesBaseName = "liquibase-gradle-plugin"
version = liquibaseGradlePluginVersion

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly "org.codehaus.groovy:groovy:${groovyVersion}"
    compileOnly "org.liquibase:liquibase-core:${liquibaseCoreVersion}"
    // implementation "org.liquibase:liquibase-standard:${liquibaseCoreVersion}"
    testImplementation "org.liquibase:liquibase-core:${liquibaseCoreVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation gradleTestKit()

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

tasks.register('sourceJar', Jar) {
    description = "An archive of the source code for Maven Central"
    archiveClassifier = "sources"
    from sourceSets.main.groovy
}

tasks.register('groovydocJar', Jar) {
    dependsOn groovydoc
    description = "An archive of the GroovyDocs for Maven Central"
    archiveClassifier = "javadoc"
    from groovydoc
}

// By default, TestKit uses the classpath at
// build/pluginUnderTestMetadata/plugin-under-test-metadata.properties, which does not include test
// classpath. This action generates a file that serves a similar purpose as
// plugin-under-test-metadata.properties, but based on the test classpath.
tasks.named("testClasses") {task ->
    doFirst {
        // Record test classpath so we can use it for our TestKit tests
        def outputDir = tasks.named("processTestResources").get().destinationDir
        outputDir.mkdirs()
        def outputFile = new File(outputDir, "test-classpath.properties")

        def testRuntimeClasspath = sourceSets.test.runtimeClasspath
        def classpathString = testRuntimeClasspath
                .collect { it.absolutePath }
                .join(File.pathSeparator)

        def props = new Properties()
        props.setProperty("test-classpath", classpathString)

        outputFile.withOutputStream { out ->
            props.store(out, "Generated by $task")
        }
    }
}

// Use JUnit Platform for tests
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs = jvmArgs + ['--add-opens=java.base/java.lang=ALL-UNNAMED']
    jvmArgs = jvmArgs + ['--add-opens=java.base/java.lang.invoke=ALL-UNNAMED']
}

// Configure the Gradle Portal plugin.  Note that the ID must match it's Gradle Plugin Portal id.
gradlePlugin {
    website = "https://github.com/liquibase/liquibase-gradle-plugin"
    vcsUrl = "https://github.com/liquibase/liquibase-gradle-plugin.git"
    plugins {
        liquibasePlugin {
            id = "org.liquibase.gradle"
            displayName = "Gradle Liquibase Plugin"
            description = "A Gradle plugin for running the Liquibase database upgrade tool."
            implementationClass = "org.liquibase.gradle.LiquibasePlugin"
            tags.set(["liquibase", "database"])
        }
    }
}

// Configure the Vanniktech publishing plugin.
mavenPublishing {
    coordinates(group, archivesBaseName, liquibaseGradlePluginVersion)
    signAllPublications()
    // The vanniktech plugin is supposed to default to Maven Central, but as of 6/21/25, it still
    // defaults to the old OSSRH site.
    publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
    pom {
        name = "Gradle Liquibase Plugin"
        description = "A Gradle plugin for running the Liquibase database upgrade tool."
        url = "https://github.com/liquibase/liquibase-gradle-plugin"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "stevesaliman"
                name = "Steven C. Saliman"
                url = "https://github.com/stevesaliman/"
            }
        }
        scm {
            url = "https://github.com/liquibase/liquibase-gradle-plugin"
            connection = "scm:https://liquibase@github.com/liquibase/liquibase-gradle-plugin"
            developerConnection = "scm:git@github.com:liquibase/liquibase-gradle-plugin.git"
        }
    }
}

// Disable PluginMarkerMaven uploads to any Maven repo (e.g., snapshots / Central)
afterEvaluate {
    tasks.withType(PublishToMavenRepository).configureEach { t ->
        def pub = t.publication
        if (pub instanceof MavenPublication && pub.name.endsWith('PluginMarkerMaven')) {
            t.enabled = false
            println("Disabled ${t.name} for ${pub.groupId}:${pub.artifactId}")
            logger.lifecycle("Disabled ${t.name} for ${pub.groupId}:${pub.artifactId}")
        }
    }
    publishing.publications.withType(MavenPublication).all {
        println "publication '$name' -> $groupId:$artifactId:$version"
    }
}
